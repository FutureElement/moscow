apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile 'junit:junit:4.12'
    compile 'org.slf4j:slf4j-api:1.7.13'
    compile 'com.google.guava:guava:19.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.5'
    compile 'org.skyscreamer:jsonassert:1.3.0'

    // option 1
    compile 'com.squareup.okhttp3:okhttp:3.1.2'
    // option 2
    compile 'org.springframework.boot:spring-boot-starter-web:1.3.2.RELEASE'
    // option 2.1
//    runtime 'org.apache.httpcomponents:httpclient:4.5.1'

    testCompile 'org.springframework:spring-test:4.2.4.RELEASE'
    testRuntime 'ch.qos.logback:logback-classic:1.1.3'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = "com.github.macdao"
archivesBaseName = "moscow"
version = "0.1.0"

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'moscow'
                packaging 'jar'
                description 'Moscow is a tool for testing provider\'s API using Moco\'s configuration file (contracts)'
                url 'https://github.com/macdao/moscow'

                scm {
                    connection 'scm:git:git@github.com:macdao/moscow.git'
                    developerConnection 'scm:git:git@github.com:macdao/moscow.git'
                    url 'git@github.com:macdao/moscow.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'macdao'
                        name 'Xi Qi'
                        email 'realxqi@gmail.com'
                    }
                }
            }
        }
    }
}

def installer = install.repositories.mavenInstaller
def deployer = uploadArchives.repositories.mavenDeployer

[installer, deployer]*.pom*.whenConfigured { pom ->
    pom.dependencies.findAll { dep -> dep.groupId == 'com.squareup.okhttp3' || dep.groupId == 'org.springframework.boot' }.each {
        it.optional = true
    }
}
